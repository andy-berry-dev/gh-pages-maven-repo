apply plugin : 'maven'

ext.depVars = {}

def depProperties = ['depGroup', 'depVersion', 'depArtifact', 'depFile']

def usageMsg = "Usage:  gradle "
def missingProperties = []
depProperties.each {
	if (!project.hasProperty(it)) {
		missingProperties += [it]
	} else {
		depVars[it] = (String) property(it)
	}
	usageMsg += "-P${it}=... "
}

if (missingProperties.size > 0) {
	throw new GradleException( "One or more properties were missing. Missing: ${missingProperties.join(' ')}.\n${usageMsg}")
}

def depFile;
def depFilePath = depVars['depFile']
[ file(depFilePath), new File(depFilePath) ].each {
	if (it.isFile()) {
		depFile = it
	}
}
if (depFile == null) {
	throw new GradleException("No file could be found at ${depFilePath} (Note: using ~ in the path won't work)")
}


artifacts {
	archives file: depFile, name: depVars['depArtifact']
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository ( url: "file://localhost${projectDir}/repo" )
            pom.version = depVars['depVersion']
            pom.artifactId = depVars['depArtifact']
            pom.groupId = depVars['depGroup']
        }
    }
}

defaultTasks 'uploadArchives'